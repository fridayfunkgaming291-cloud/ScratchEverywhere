set(SE_DEFAULT_OUTPUT_NAME "scratch-vita")

set(SE_RENDERER_VALID_OPTIONS "sdl1" "sdl2" "sdl3")
set(SE_AUDIO_ENGINE_VALID_OPTIONS "sdl1" "sdl2" "sdl3")
set(SE_DEPS_VALID_OPTIONS "fallback" "system")
set(SE_LUA_BACKEND_VALID_OPTIONS "fallback" "luajit")

set(SE_CACHING_DEFAULT ON)
set(SE_DECTALK_DEFAULT ON)

set(SE_ALLOW_CMAKERC OFF)
set(SE_ALLOW_CLOUDVARS OFF)
set(SE_ALLOW_DOWNLOAD OFF)

set(SE_PLATFORM_DEFINITIONS "VITA")
set(SE_PLATFORM "vita")

set(SE_HAS_THREADS ON)

set(SE_HAS_TOUCH TRUE)
set(SE_HAS_MOUSE FALSE)
set(SE_HAS_KEYBOARD FALSE)
set(SE_HAS_CONTROLLER TRUE)

set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -ffast-math -fomit-frame-pointer -ffunction-sections -fdata-sections -fno-exceptions -fno-rtti")
include(CheckIPOSupported)
check_ipo_supported(RESULT ipo_supported)
if(ipo_supported)
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE TRUE)
endif()

macro(package_platform)
	include("${VITASDK}/share/vita.cmake" REQUIRED)

	target_link_options(scratch-everywhere PRIVATE "-Wl,--defsym=__sce_headroom=0x10000")

	target_link_libraries(se-interface INTERFACE SceAppUtil_stub)

	target_link_libraries(se-interface INTERFACE SceShaccCgExt SceShaccCg_stub stdc++ taihen_stub_weak)

	if(EXISTS ${CMAKE_SOURCE_DIR}/romfs/project.sb3)
		set(PROJ_FILE FILE ${CMAKE_SOURCE_DIR}/romfs/project.sb3 project.sb3)
	else()
		set(PROJ_FILE "")
	endif()

	set(VITA_GFXFILES)
	foreach(file IN LISTS GFXFILES)
		file(RELATIVE_PATH REL_PATH "${CMAKE_SOURCE_DIR}/gfx" "${file}")
		set(DEST_PATH "gfx/${REL_PATH}")
		list(APPEND VITA_GFXFILES FILE "${file}" "${DEST_PATH}")
	endforeach()

	vita_create_self("${SE_OUTPUT_NAME}.self" scratch-everywhere)
	vita_create_vpk("${SE_OUTPUT_NAME}.vpk" "${SE_APP_TITLEID}" "${SE_OUTPUT_NAME}.self"
		VERSION "01.00"
		NAME "${SE_APP_NAME}"
		FILE ${CMAKE_SOURCE_DIR}/gfx/vita/icon0.png sce_sys/icon0.png
		FILE ${CMAKE_SOURCE_DIR}/gfx/vita/livearea/contents/bg.png sce_sys/livearea/contents/bg.png
		FILE ${CMAKE_SOURCE_DIR}/gfx/vita/livearea/contents/template.xml sce_sys/livearea/contents/template.xml
		FILE ${CMAKE_SOURCE_DIR}/gfx/vita/livearea/contents/startup.png sce_sys/livearea/contents/startup.png
		${VITA_GFXFILES}
		${PROJ_FILE}
	)
endmacro()
